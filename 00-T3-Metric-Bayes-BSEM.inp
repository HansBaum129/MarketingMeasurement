TITLE: BSEM with metric data

DATA: file = mvs.dat;

VARIABLE: 
  names = idnum satopt ars ars01 im mvs1-mvs18;
  usevar = mvs1-mvs18;

  missing = all(-99);

ANALYSIS: 
  ESTIMATOR=BAYES;
  ALGORITHM=GIBBS(RW);
  THIN = 1;
  BITERATIONS = 500000 0;
  BSEED = 100;
  FBITER = (10000);
  PROCESSORS=2;
  !STVAL = ML;

MODEL:

  SUC BY mvs1*1 (l1)
         mvs2 (l2) 
         mvs3 (l3) 
         mvs4 (l4) 
         mvs5 (l5) 
         mvs6 (l6);
  CEN BY mvs7*1 (l7)
         mvs8 (l8) 
         mvs9 (l9) 
         mvs10 (l10) 
         mvs11 (l11) 
         mvs12 (l12);
  HAP BY mvs13*1 (l13) 
         mvs14 (l14) 
         mvs15 (l15) 
         mvs16 (l16) 
         mvs17 (l17) 
         mvs18 (l18); 

  SUC@1;
  CEN@1;
  HAP@1;

  SUC WITH CEN;
  SUC WITH HAP;
  CEN WITH HAP;

  mvs1 (ev1);
  mvs2 (ev2);
  mvs3 (ev3);
  mvs4 (ev4);
  mvs5 (ev5);
  mvs6 (ev6);
  mvs7 (ev7);
  mvs8 (ev8);
  mvs9 (ev9);
  mvs10 (ev10);
  mvs11 (ev11);
  mvs12 (ev12);
  mvs13 (ev13);
  mvs14 (ev14);
  mvs15 (ev15);
  mvs16 (ev16);
  mvs17 (ev17);
  mvs18 (ev18);

  SUC BY mvs7-mvs18 (a1-a12);
  CEN BY mvs1-mvs6 (b1-b6);
  CEN BY mvs13-mvs18 (b7-b12);
  HAP BY mvs1-mvs12 (C1-C12);

MODEL PRIORS:
a1-a12 ~ N(0,.01);
b1-b12 ~ N(0,.01);
c1-c12 ~ N(0,.01);

OUTPUT: TECH1 TECH4 svalues CINTERVAL standardized; 
